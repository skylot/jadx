plugins {
    id 'edu.sc.seis.launch4j' version '2.4.3'
    id 'com.github.johnrengelman.shadow' version '2.0.2'
}

apply plugin: 'application'

mainClassName = 'jadx.gui.JadxGUI'

dependencies {
    compile(project(":jadx-core"))
    compile(project(":jadx-cli"))
    compile 'com.fifesoft:rsyntaxtextarea:2.6.1'
    compile 'com.google.code.gson:gson:2.8.2'
    compile files('libs/jfontchooser-1.0.5.jar')
    compile 'hu.kazocsaba:image-viewer:1.2.3'

    compile 'org.apache.commons:commons-lang3:3.7'

    compile 'io.reactivex.rxjava2:rxjava:2.1.13'
    compile "com.github.akarnokd:rxjava2-swing:0.2.12"
}

applicationDistribution.with {
    into('') {
        from '../'
        include 'README.md'
        include 'NOTICE'
        include 'LICENSE'
    }
}

jar {
    manifest {
        attributes(
                "Main-Class": mainClassName,
                "Class-Path": configurations.compile.collect { it.getName() }.join(' ')
        )
    }
}

startScripts {
    defaultJvmOpts = ['-Xms128M', '-Xmx4g']
    doLast {
        def str = windowsScript.text
        str = str.replaceAll('java.exe', 'javaw.exe')
        str = str.replaceAll('"%JAVA_EXE%" %DEFAULT_JVM_OPTS%',
                'start "jadx-gui" /B "%JAVA_EXE%" %DEFAULT_JVM_OPTS%')
        windowsScript.text = str
    }
}

launch4j {
    mainClassName = 'jadx.gui.JadxGUI'
    copyConfigurable = project.tasks.shadowJar.outputs.files
    jar = "lib/${project.tasks.shadowJar.archiveName}"
//  icon = "${projectDir}/icons/myApp.ico"
    outfile = "jadx-gui-${version}.exe"
    copyright = 'Skylot'
    windowTitle = 'jadx'
    jreRuntimeBits = "64"
    initialHeapPercent = 5
    maxHeapSize = 4096
    maxHeapPercent = 70
}

test {
    jacoco {
        enabled = false
    }
}
