apply plugin: 'application'

mainClassName = 'jadx.gui.JadxGUI'

dependencies {
    compile(project(":jadx-core"))
    compile(project(":jadx-cli"))
    compile 'com.fifesoft:rsyntaxtextarea:2.5.8'
    compile 'com.google.code.gson:gson:2.3.1'
    compile files('libs/jfontchooser-1.0.5.jar')
    compile 'hu.kazocsaba:image-viewer:1.2.3'
}

applicationDistribution.with {
    into('') {
        from '../'
        include 'README.md'
        include 'NOTICE'
        include 'LICENSE'
    }
}

jar {
    manifest {
        attributes(
                "Main-Class": mainClassName,
                "Class-Path": configurations.compile.collect { it.getName() }.join(' ')
        )
    }
}

startScripts {
    defaultJvmOpts = [ '-Xms2g', '-Xmx4g' ]
    doLast {
        def str = windowsScript.text
        str = str.replaceAll('java.exe', 'javaw.exe')
        str = str.replaceAll('"%JAVA_EXE%" %DEFAULT_JVM_OPTS%',
                'start "jadx-gui" /B "%JAVA_EXE%" %DEFAULT_JVM_OPTS%')
        windowsScript.text = str
    }
}

test {
    jacoco {
        enabled = false
    }
}
